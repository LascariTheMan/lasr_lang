grammar dk.sdu.mdsd.Lasr_lang with org.eclipse.xtext.common.Terminals

generate lasr_lang "http://www.sdu.dk/mdsd/lasr_lang"

Model:
	VirtualParameters+=VirtualParameters* virtualIntents+=VirtualIntent* intents+=Intent* entitytypes+=EntityType* 
;

// Intents---------------------
Intent:
	{Intent} 'intent' name=ID (':' vi+=[VirtualIntent](',' vi+=[VirtualIntent]))? '{'values+=IntentValue*'}'
;

IntentValue:
    iv=IntentOptional
;

IntentOptional:
	KeyValue | List
;

KeyValue:
	v='action' ':' name=ID
;

List:
	TrainingPhrases | Parameters | Messages
;

TrainingPhrases:
	v='trainingPhrases' '{' phrases+=Phrase*'}'
;

Phrase:
	'phrase' ':' sentences+=Sentence(',' sentences+=Sentence)*
;

Sentence:
	{Sentence} (words+=Words)+ ('('entity=ID')')?
;

Words:
	name = STRING
;

Parameters:
	 v='parameters' (':' vp=[VirtualParameters])? '{'parameters+=Parameter*'}'
;

Parameter:
	req=('required')? 'parameter' name=ID '{' 'entityType' ':' type=ID  prompts+=Prompt* '}'
;

Prompt:
	{Prompt} ('prompt' ':' (words+=Words(',' words+=Words)*)+)
;

Messages:
	{Messages} v='messages' '{' messages+=Words(',' messages+=Words)* '}'
;

// VirtualIntent ---------------------
VirtualIntent:
	'virtual' 'intent' name = ID '{' virtualValues+=List* '}'
;

// VirtualParameters -------------------
VirtualParameters:
	'virtual' 'parameters' name = ID '{' virtualParameters+=Parameter* '}'
;

// EntityType -----------------------
EntityType:
	'entityType' name=ID '{' a='autoExpansion' ':' exp=('true'|'false') entities+=Entities+ '}'	
;

Entities:
	'entities' '{' entity+=Entity+ '}'
;

Entity:
	'entity' '{' 'value' ':' value = STRING 'synonym' ':' syn+=Synonym(',' syn+=Synonym)* '}'
;

Synonym:
	syn = STRING
;