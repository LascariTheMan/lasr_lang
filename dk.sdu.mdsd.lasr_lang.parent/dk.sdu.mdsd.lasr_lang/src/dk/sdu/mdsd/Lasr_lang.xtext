grammar dk.sdu.mdsd.Lasr_lang with org.eclipse.xtext.common.Terminals

generate lasr_lang "http://www.sdu.dk/mdsd/lasr_lang"

Model:
	abstractParameters+=GeneralAbstractParameter* abstractIntents+=AbstractIntent* intents+=Intent* entitytypes+=EntityType*
;

// Abstract Intent -------------

AbstractIntent:
	'abstract' 'intent' name=ID '{' values+=AbstractList* '}'
;

AbstractList:
	TrainingPhrases | AbstractIntentParameters | Messages
;

AbstractIntentParameters:
	{AbstractIntentParameters} 'parameters' '{'parameters+=AbstractIntentParameter*'}'
;

AbstractIntentParameter:
	ParameterExtension | Parameter
;

ParameterExtension:
	req=('required')? 'parameter' 'extends' name=ID '{' prompts+=Prompt* '}'
;

GeneralAbstractParameter:
	'abstract' 'parameter' name=ID '{' 'entityType' ':' type=ID  prompts+=Prompt* '}'
;

// Intents----------------------
Intent:
	{Intent} 'Intent' name=ID ('extends' toExtend=ID ('(' injections+=Injection(',' injections+=Injection)* ')')?)? '{' values+=IntentValue* '}'
;

Injection:
	key=ID ':' value=ID
;

IntentValue:
    iv=IntentOptional
;

IntentOptional:
	KeyValue | List
;

KeyValue:
	v='action' ':' name=ID
;

List:
	TrainingPhrases | Parameters | Messages
;

TrainingPhrases:
	v='trainingPhrases' '{' phrases+=Phrase+'}'
;

Phrase:
	'phrase' ':' sentences+=Sentence(',' sentences+=Sentence)*
;

Sentence:
	{Sentence} (words+=Words)+ ('('entity=ID')')?
;

Words:
	name = STRING
;

Parameters:
	v='parameters' '{'parameters+=Parameter* (promptExtensions+=PromptExtension)?'}'
;

Parameter:
	req=('required')? 'parameter' name=ID '{' 'entityType' ':' type=ID  prompts+=Prompt* '}'
;

PromptExtension:
	name=ID '{' prompts+=Prompt* '}'
;

Prompt:
	{Prompt} ('prompt' ':' (words+=Words(',' words+=Words)*)+)
;

Messages:
	{Messages} v='messages' '{' messages+=Words(',' messages+=Words)* '}'
;

// EntityType -----------------
EntityType:
	'EntityType' name=ID '{' a='autoExpansion' ':' exp=('true'|'false') entities+=Entities+ '}'	
;

Entities:
	'entities' '{' entity+=Entity+ '}'
;

Entity:
	'entity' '{' 'value' ':' value = STRING 'synonym' ':' syn+=Synonym(',' syn+=Synonym)* '}'
;

Synonym:
	syn = STRING
;